buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:1.5.2.RELEASE"
        classpath 'gradle.plugin.org.openrepose:gradle-jaxb-plugin:2.2.3'
    }
}

group 'com.mbpolan.retrorealms'
version '1.0.0-SNAPSHOT'

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'org.openrepose.gradle.plugins.jaxb'

jaxb {
    xsdDir = "${project.projectDir}/src/main/schema"

    xjc {
        destinationDir = 'build/generated-sources/jaxb'
        generatePackage = 'com.mbpolan.retrorealms.tmx'
    }
}

sourceSets {
    main {
        java {
            srcDirs = ["${project.projectDir}/src/main/java", "${buildDir}/generated-sources/jaxb"]
        }
        resources {
            // package the client distributable files
            srcDir "${project.projectDir}/../client/dist"
        }
    }
}

repositories {
    mavenCentral()
}

configurations {
    compile.exclude module: 'spring-boot-starter-logging'
}

dependencies {
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-log4j2'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-websocket'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
    compile group: 'org.springframework', name: 'spring-messaging'
    compile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: '2.7.0'
    compile group: 'com.h2database', name: 'h2'

    jaxb 'com.sun.xml.bind:jaxb-xjc:2.2.7-b41'
    jaxb 'com.sun.xml.bind:jaxb-impl:2.2.7-b41'
    jaxb 'javax.xml.bind:jaxb-api:2.2.7'

    testCompile group: 'junit', name: 'junit', version: '4.11'
}

compileJava.dependsOn 'xjc'

task wrapper(type: Wrapper) {
    gradleVersion = '2.9'
}